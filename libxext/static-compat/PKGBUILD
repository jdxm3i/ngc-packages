# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=libxext
pkgname=static-compat-$_pkgname
pkgver=1.3.5
pkgrel=1
pkgdesc="X11 miscellaneous extensions library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('static-compat-libx11')
makedepends=('xorg-util-macros' 'static-compat-xorgproto' 'static-compat-configure')
options=(!emptydirs staticlibs)
source=(${url}/releases/individual/lib/libXext-${pkgver}.tar.xz{,.sig})
sha512sums=('fd9805982dabfb14b1526c0cb71f94508003fa3fcd60560d387465afd891c1dca61927ae86278436d53f4b840cacd1f412071fdc3dce1d6f730610a9c163d59b'
            'SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # "Peter Hutterer (Who-T) <office@who-t.net>"
validpgpkeys+=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  source static-compat-environment
  cd libXext-${pkgver}
  static-compat-configure --sysconfdir=/etc --disable-shared --enable-static
  make
}

package() {
  source static-compat-environment
  cd libXext-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -r "$pkgdir/$static_compat_prefix"/share/{doc,man}

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

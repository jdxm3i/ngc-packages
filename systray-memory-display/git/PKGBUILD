# Maintainer: Martchus <martchus@gmx.net>

# All my PKGBUILDs are managed at https://github.com/Martchus/PKGBUILDs where
# you also find the URL of a binary repository.

_reponame=Systray-Memory-Display
pkgname=systray-memory-display-git
_name=${pkgname%-git}
pkgver=173.e4a05d6
pkgrel=1
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
pkgdesc='Simple but useful application for monitoring your system memory usage'
license=('GPL')
depends=()
makedepends=('qt5-base' 'git')
provides=("${_name}")
conflicts=("${_name}")
url="https://github.com/doublejim/${_reponame}"
source=("${_reponame}::git+https://github.com/doublejim/${_reponame}.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_reponame"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_reponame"
  qmake-qt5
  make
}

package() {
  cd "$srcdir/$_reponame"
  install -Dm0755 "memtray" "$pkgdir/usr/bin/memtray"
}

# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=libxcomposite
pkgname=static-compat-$_pkgname
pkgver=0.4.6
pkgrel=1
pkgdesc="X11 Composite extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('static-compat-libxfixes')
makedepends=('static-compat-configure' 'static-compat-xorgproto')
options=(staticlibs !docs !emptydirs)
source=("${url}/releases/individual/lib/libXcomposite-${pkgver}.tar.xz"{,.sig})
sha512sums=('f35a0da8f9f22fd788eee01209557fe3a1b1abdc9744f76330ba42b21b6e3af5464ca4451c33415856417c30da5aa5624e3707980c644327249bfeda5a62a59b'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  source static-compat-environment
  cd libXcomposite-${pkgver}
  static-compat-configure --enable-static --disable-shared
  make
}

package() {
  source static-compat-environment
  cd libXcomposite-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -r "$pkgdir/$static_compat_prefix"/share/man

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}

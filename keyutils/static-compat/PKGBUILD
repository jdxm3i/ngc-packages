# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

_pkgname=keyutils
pkgname=static-compat-$_pkgname
_tag='5678a1aae8834b5c16b5ed7dc72ef8836a29e122' # git rev-parse v${pkgver}
pkgver=1.6.3
pkgrel=1
pkgdesc='Linux Key Management Utilities'
arch=('x86_64')
url='https://www.kernel.org/'
license=('GPL2' 'LGPL2.1')
depends=('glibc-static-compat')
makedepends=('git' 'static-compat-environment')
options=(!emptydirs staticlibs)
#validpgpkeys=('A86E54B0D5E1B4E0AB7C640FFBB7576BA7CB0B6B') # David Howells <dhowells@redhat.com>
#source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/dhowells/keyutils.git#tag=${_tag}?signed"
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/dhowells/keyutils.git#tag=${_tag}"
        'request-key.conf.patch' 'reproducible.patch')
sha256sums=('SKIP'
            '203c602c61ed94ccd423a0a453d74143d678c641a9a4486367576ee8af2cb8d6'
            '7bb7400b2b8c8f0288c86ec9191f8964a1e682745a204013d5fc7c2e1a253d8e')

prepare() {
  source static-compat-environment
  cd "${_pkgname}"
  # fix paths of binaries in /etc/request-key.conf
  patch -Np0 -i ../request-key.conf.patch

  # make keyutils reproducible
  patch -Np1 -i ../reproducible.patch
}

build() {
  source static-compat-environment
  cd "${_pkgname}"
  make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}" \
    PREFIX="$static_compat_prefix" SBINDIR="$static_compat_prefix"/bin BINDIR="$static_compat_prefix"/bin \ INCLUDEDIR="$static_compat_prefix"/include \
    NO_SOLIB=1
}

package() {
  source static-compat-environment
  cd "${_pkgname}"
  make DESTDIR="${pkgdir}" \
    PREFIX="$static_compat_prefix" SBINDIR="$static_compat_prefix"/bin BINDIR="$static_compat_prefix"/bin \ INCLUDEDIR="$static_compat_prefix"/include LIBDIR="$static_compat_prefix"/lib \
    USRLIBDIR="$static_compat_prefix"/lib \
    NO_SOLIB=1 install pkgconfig
  install -Dt "$pkgdir/$static_compat_prefix"/lib/pkgconfig -m644 libkeyutils.pc
  rm -r "${pkgdir}/$static_compat_prefix"/bin
  rm -r "${pkgdir}"/usr/share
  rm -r "${pkgdir}"/etc
}

# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=libxxf86vm
pkgname=static-compat-$_pkgname
pkgver=1.1.5
pkgrel=1
pkgdesc="X11 XFree86 video mode extension library"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('static-compat-libxext')
makedepends=('static-compat-configure' 'static-compat-xorgproto')
options=(staticlibs !emptydirs)
source=(${url}/releases/individual/lib/libXxf86vm-${pkgver}.tar.xz{,.sig})
sha512sums=('68226fcb9773a583ea417ab413146203dd7db6ff8a4d388285da972353355e11e179861a77c9fa284aefac025bb3bbf5a9537fa0bf246c782e0116a667c9d9b6'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  source static-compat-environment
  cd libXxf86vm-${pkgver}
  static-compat-configure --disable-shared --enable-static
  make
}

package()
{
  source static-compat-environment
  cd libXxf86vm-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -r "$pkgdir/$static_compat_prefix"/share/man

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

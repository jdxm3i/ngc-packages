# Maintainer: Vojtech Kral <vojtech_kral^hk>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# Contributor: Martchus <martchus@gmx.net>

pkgname=kdeconnect-git
pkgver=r1362.04713de
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
arch=(i686 x86_64)
url='https://community.kde.org/KDEConnect'
license=(GPL2)
depends=(hicolor-icon-theme kcmutils kwayland libfakekey qca-qt5)
makedepends=(extra-cmake-modules git python)
optdepends=('sshfs: remote filesystem browser' 'kde-cli-tools: configuration UI')
provides=(kdeconnect)
conflicts=(kdeconnect-frameworks kdeconnect)
source=('git://anongit.kde.org/kdeconnect-kde.git')
sha256sums=('SKIP')

pkgver() {
  cd kdeconnect-kde
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdeconnect-kde \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
